import web/runtime/abi
import web/runtime/memory
import web/runtime/events
import web/std/web_dom

type
    WebAppInit = fn ()
    WebAppMount = fn (root: WebNode): int32
    WebAppUpdate = fn (handle: int32)
    WebAppUnmount = fn (handle: int32)

var
    appInit: WebAppInit
    appMount: WebAppMount
    appUpdate: WebAppUpdate
    appUnmount: WebAppUnmount
    lastMountHandle: int32 = 0

fn registerApp(init: WebAppInit, mount: WebAppMount, update: WebAppUpdate, unmount: WebAppUnmount) =
    appInit = init
    appMount = mount
    appUpdate = update
    appUnmount = unmount

fn cheng_web_abi_version(): int32 =
    return WebAbiVersion

fn cwc_init() =
    if appInit != nil:
        appInit()

fn cwc_mount(rootPtr: void*, rootLen: int32): int32 =
    var root: WebNode = 0
    if rootLen > 0:
        let selector = readString(rootPtr, rootLen)
        if selector != nil && len(selector) > 0:
            root = domQuerySelector(selector)
    if root == 0:
        root = domGetRoot()
    if appMount == nil:
        return 0
    let handle = appMount(root)
    lastMountHandle = handle
    return handle

fn cwc_update(handle: int32) =
    if appUpdate == nil:
        return
    let target = if handle == 0: lastMountHandle else: handle
    if target == 0:
        return
    appUpdate(target)

fn cwc_unmount(handle: int32) =
    if appUnmount == nil:
        return
    let target = if handle == 0: lastMountHandle else: handle
    if target == 0:
        return
    appUnmount(target)
