import cheng/web/runtime/abi
import cheng/web/runtime/signals
import cheng/web/runtime/events
import cheng/web/runtime/view
import cheng/web/std/web_dom

type
    BindingHandle =
        effect: Effect
        handler: WebHandler

fn bindText(node: WebNode, getter: fn (): str): Effect =
    if node == 0 || getter == nil:
        return nil
    let apply = fn () =
        domTextSet(node, getter())
    return effect(apply)

fn bindAttr(node: WebNode, name: str, getter: fn (): str): Effect =
    if node == 0 || name == nil || getter == nil:
        return nil
    let apply = fn () =
        domSetAttr(node, name, getter())
    return effect(apply)

fn bindStyle(node: WebNode, name: str, getter: fn (): str): Effect =
    if node == 0 || name == nil || getter == nil:
        return nil
    let apply = fn () =
        domSetStyle(node, name, getter())
    return effect(apply)

fn bindProp(node: WebNode, name: str, getter: fn (): str): Effect =
    if node == 0 || name == nil || getter == nil:
        return nil
    let apply = fn () =
        domSetProp(node, name, getter())
    return effect(apply)

fn bindClass(node: WebNode, getter: fn (): str): Effect =
    if node == 0 || getter == nil:
        return nil
    let apply = fn () =
        domSetAttr(node, "class", getter())
    return effect(apply)

fn bindValue(node: WebNode, getter: fn (): str, setter: fn (value: str)): BindingHandle =
    var res: BindingHandle
    if node == 0:
        return res
    if getter != nil:
        let apply = fn () =
            domSetProp(node, "value", getter())
        res.effect = effect(apply)
    if setter != nil:
        let handler = fn (ev: WebEvent) =
            let value = eventValue(ev)
            setter(value)
        res.handler = registerDomEvent(node, "input", handler)
    return res

fn bindChecked(node: WebNode, getter: fn (): bool, setter: fn (value: bool)): BindingHandle =
    var res: BindingHandle
    if node == 0:
        return res
    if getter != nil:
        let apply = fn () =
            if getter():
                domSetProp(node, "checked", "true")
            else:
                domSetProp(node, "checked", "")
        res.effect = effect(apply)
    if setter != nil:
        let handler = fn (ev: WebEvent) =
            let value = eventChecked(ev)
            setter(value)
        res.handler = registerDomEvent(node, "change", handler)
    return res

fn blockBindText(block: NodeBlock, node: WebNode, getter: fn (): str) =
    let eff = bindText(node, getter)
    blockTrackEffect(block, eff)

fn blockBindAttr(block: NodeBlock, node: WebNode, name: str, getter: fn (): str) =
    let eff = bindAttr(node, name, getter)
    blockTrackEffect(block, eff)

fn blockBindStyle(block: NodeBlock, node: WebNode, name: str, getter: fn (): str) =
    let eff = bindStyle(node, name, getter)
    blockTrackEffect(block, eff)

fn blockBindProp(block: NodeBlock, node: WebNode, name: str, getter: fn (): str) =
    let eff = bindProp(node, name, getter)
    blockTrackEffect(block, eff)

fn blockBindClass(block: NodeBlock, node: WebNode, getter: fn (): str) =
    let eff = bindClass(node, getter)
    blockTrackEffect(block, eff)

fn blockBindValue(block: NodeBlock, node: WebNode, getter: fn (): str, setter: fn (value: str)) =
    let res = bindValue(node, getter, setter)
    blockTrackEffect(block, res.effect)
    if res.handler != 0:
        blockTrackHandler(block, node, "input", res.handler)

fn blockBindChecked(block: NodeBlock, node: WebNode, getter: fn (): bool, setter: fn (value: bool)) =
    let res = bindChecked(node, getter, setter)
    blockTrackEffect(block, res.effect)
    if res.handler != 0:
        blockTrackHandler(block, node, "change", res.handler)
