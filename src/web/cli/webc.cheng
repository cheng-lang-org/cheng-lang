import cmdline
import web/compiler/driver
import std/os
import std/strings

fn printLine(text: str) =
    writeLine(get_stdout(), text)

fn printErr(text: str) =
    writeLine(get_stderr(), text)

fn printUsage() =
    printLine("usage: webc <file.cwc> [--out:<file.cheng>] [--css:<file.css>] [--map]")

fn hasPrefix(s: str, prefix: str): bool =
    if s == nil:
        return false
    if len(prefix) > len(s):
        return false
    for i in 0..<len(prefix):
        if s[i] != prefix[i]:
            return false
    return true

fn main(argc: int32, argv: str*): int32 =
    cmdline.__cheng_setCmdLine(argc, void*(argv))
    let count: int32 = cmdline.paramCount()
    if count <= 0:
        printUsage()
        return 1
    var inputPath: str = ""
    var outPath: str = ""
    var cssPath: str = ""
    var mapEnabled: bool = false
    for i in 0..<count:
        let arg: str = cmdline.paramStr(i + 1)
        if arg == "--help" || arg == "-h":
            printUsage()
            return 0
        if hasPrefix(arg, "--out:"):
            outPath = substr(arg, 6, len(arg) - 6)
        elif hasPrefix(arg, "--css:"):
            cssPath = substr(arg, 6, len(arg) - 6)
        elif arg == "--map":
            mapEnabled = true
        elif len(inputPath) == 0:
            inputPath = arg
        else:
            printErr("unknown arg: " + arg)
            return 1
    if len(inputPath) == 0:
        printUsage()
        return 1
    let res = compileSfcFileEx(inputPath, outPath, cssPath, mapEnabled)
    if ! res.ok:
        printErr("webc failed: " + res.error)
        return 1
    printLine("webc ok: " + res.outPath)
    if len(res.cssPath) > 0:
        printLine("webc css: " + res.cssPath)
    if len(res.mapPath) > 0:
        printLine("webc map: " + res.mapPath)
    if len(res.cssMapPath) > 0:
        printLine("webc css map: " + res.cssMapPath)
    return 0
