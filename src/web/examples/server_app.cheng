import cheng/web/runtime/server_app
import cheng/web/std/server
import cheng/web/router/router as rtr
import cheng/web/std/web_url

var
    appRouter: ServerRouter
    appAssets: StaticAssets
    appHandler: ServerHandler
    staticHandler: ServerHandler

fn helloHandler(req: HttpRequest, params: rtr.RouteParams): HttpResponse =
    var name = rtr.paramGet(params, "name")
    if len(name) == 0:
        name = "world"
    let query = requestQueryParams(req)
    let lang = urlQueryGet(query, "lang")
    var suffix = ""
    if len(lang) > 0:
        suffix = " (" + lang + ")"
    return textResponse("Hello " + name + suffix)

fn pingHandler(req: HttpRequest, params: rtr.RouteParams): HttpResponse =
    req
    params
    return jsonResponse("{\"ok\":true}")

fn appSetup() =
    if appHandler != nil:
        return
    appRouter = newServerRouter()
    addRoute(appRouter, hmGet, "/hello/[name]", helloHandler)
    addRoute(appRouter, hmGet, "/api/ping", pingHandler)
    appAssets = newStaticAssets()
    addAsset(appAssets, "/health.txt", "text/plain; charset=utf-8", "ok")
    staticHandler = serveAssets(appAssets)
    let routerHandler = fn (req: HttpRequest): HttpResponse =
        return routeRequest(appRouter, req)
    appHandler = applyMiddleware(routerHandler, [])

fn handleRequest(req: HttpRequest): HttpResponse =
    appSetup()
    var res = appHandler(req)
    if res.status == 404 && staticHandler != nil:
        res = staticHandler(req)
    return res

registerServerApp(handleRequest)
